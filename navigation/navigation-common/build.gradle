/*
 * Copyright (C) 2016 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */


import androidx.build.JetbrainsAndroidXPlugin
import androidx.build.KmpPlatformsKt
import androidx.build.PlatformIdentifier
import androidx.build.Publish
import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType
import org.jetbrains.kotlin.konan.target.Family

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    alias(libs.plugins.kotlinSerialization)
    id("JetbrainsAndroidXPlugin")
}

JetbrainsAndroidXPlugin.applyAndConfigure(project)

def jsEnabled = KmpPlatformsKt.enableJs(project)
def wasmEnabled = KmpPlatformsKt.enableWasm(project)

androidXMultiplatform {
    android()
    desktop()
    mac()
    linux()
    ios()
    js()
    wasm()

    defaultPlatform(PlatformIdentifier.ANDROID)
}

kotlin {
    js {
        browser()
    }
    wasmJs()

    sourceSets {
        commonMain {
            dependencies {
                api project(":annotation:annotation")
                api project(":collection:collection")
                api project(":core:core-bundle")
                api project(":lifecycle:lifecycle-common")
                api project(":lifecycle:lifecycle-runtime")
                api project(":lifecycle:lifecycle-viewmodel")
                api project(":lifecycle:lifecycle-viewmodel-savedstate")
                api project(":savedstate:savedstate")

                api(libs.kotlinStdlib)
                implementation(libs.kotlinSerializationCore)
            }
        }

        commonTest {
            dependencies {
                implementation(libs.kotlinStdlib)
                implementation(libs.kotlinCoroutinesTest)
                implementation(libs.kotlinTest)
            }
        }

        jvmMain {
            dependsOn(commonMain)
        }

        androidMain {
            dependsOn(jvmMain)
            dependencies {
                implementation("androidx.core:core-ktx:1.1.0")
                implementation("androidx.profileinstaller:profileinstaller:1.3.0")
            }
        }

        androidUnitTest {
            dependsOn(commonTest)
            dependencies {
                // TODO: uncomment once KMP for navigation-runtime is ready
                //implementation(project(":navigation:navigation-testing"))
                implementation("androidx.arch.core:core-testing:2.2.0")
                implementation(libs.junit)
                implementation(libs.mockitoCore4)
                implementation(libs.truth)
            }
        }

        androidInstrumentedTest {
            dependsOn(commonTest)
            dependencies {
                implementation(libs.kotlinTestJunit)
                implementation(libs.testExtJunit)
                implementation(libs.testCore)
                implementation(libs.testRunner)
                implementation(libs.espressoCore)
                implementation(libs.truth)
                implementation(libs.multidex)

                // CustomMessageMissingMethodException: Could not find method implementation() for arguments
                // implementation(libs.mockitoCore, excludes.bytebuddy)
                // implementation(libs.dexmakerMockito, excludes.bytebuddy)
                implementation("org.mockito:mockito-core:2.25.0", excludes.bytebuddy)
                implementation("com.linkedin.dexmaker:dexmaker-mockito:2.28.3", excludes.bytebuddy)
            }
        }


        jbMain.dependsOn(commonMain)
        desktopMain {
            dependsOn(jvmMain)
            dependsOn(jbMain)
        }
        nonJvmMain.dependsOn(jbMain)
        nativeMain.dependsOn(nonJvmMain)
        darwinMain.dependsOn(nativeMain)
        linuxMain.dependsOn(nativeMain)

        if (jsEnabled || wasmEnabled) {
            webMain.dependsOn(nonJvmMain)
        }

        targets.all { target ->
            if (target.platformType == KotlinPlatformType.native) {
                target.compilations["main"].defaultSourceSet {
                    def konanTargetFamily = target.konanTarget.family
                    if (konanTargetFamily == Family.OSX || konanTargetFamily == Family.IOS) {
                        dependsOn(darwinMain)
                    } else if (konanTargetFamily == Family.LINUX) {
                        dependsOn(linuxMain)
                    } else {
                        throw new GradleException("unknown native target ${target}")
                    }
                }
            } else if (target.platformType in [
                    KotlinPlatformType.js,
                    KotlinPlatformType.wasm
            ]) {
                target.compilations["main"].defaultSourceSet {
                    dependsOn(webMain)
                }
            }
        }
    }
}

android {
    buildTypes.all {
        consumerProguardFiles "proguard-rules.pro"
    }
    defaultConfig {
        multiDexEnabled true
    }
    namespace "androidx.navigation.common"
}

androidx {
    name = "Navigation Common"
    publish = Publish.SNAPSHOT_AND_RELEASE
    inceptionYear = "2017"
    description = "Android Navigation-Common"
    metalavaK2UastEnabled = true
}
